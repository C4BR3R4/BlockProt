import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'org.jetbrains.kotlin.jvm' version '1.5.0'
    id 'java'
    id 'application'
}

static def gitTagName() {
    def tag = ""
    def proc = "git describe --tags --abbrev=0".execute()
    proc.in.eachLine { line -> tag = line }
    proc.waitFor()
    return tag
}

static def gitBranchName() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.waitFor()
    return branch
}

def version = "${project.name}-${gitTagName()}-${gitBranchName()}"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {
        name = 'Spigot'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
    maven {
        name = 'CodeMC'
        url = 'https://repo.codemc.org/repository/maven-public/'
        content {
            includeGroup 'de.tr7zw'
            includeGroup 'org.bstats'
            includeGroup 'net.wesjd'
        }
    }
    mavenCentral()
}

dependencies {
    implementation 'de.tr7zw:item-nbt-api:2.7.1' // item-nbt-api

    // Use anvils as inventories. They're stupid and require NMS, making renaming much easier.
    implementation 'net.wesjd:anvilgui:1.5.0-SNAPSHOT'

    api 'org.bstats:bstats-bukkit:2.2.1'

    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT' // Spigot
}

jar {
    setArchivesBaseName(version)
}

shadowJar {
    archiveFileName = "${version}-${getArchiveClassifier().get()}.jar"

    relocate 'de.tr7zw.changeme.nbtapi', 'de.sean.blockprot.nbtapi'
    relocate 'org.bstats', 'de.sean.blockprot.metrics'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        it.setDuplicatesStrategy(DuplicatesStrategy.INCLUDE)
        filter ReplaceTokens, tokens: [version: gitTagName()]
    }
}

application {
    getMainClass().set('de.sean.blockprot.BlockProt')
}

compileKotlin {
    kotlinOptions.useIR = true
    kotlinOptions.jvmTarget = "1.8"
}
